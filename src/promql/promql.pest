WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

promql = _{ SOI ~ ((metric_name ~ label_matchers) | label_matchers | metric_name) ~ range_vector? ~ EOI }

label_matchers = _{ "{" ~ label_matcher ~ ("," ~  label_matcher)* ~ "}" }
label_matcher = { name ~ operator ~ string }

metric_name = @{ name }
name = { (ASCII_ALPHANUMERIC | "_")+ }

range_vector = { "[" ~ time ~ duration ~ "]"}
time = { ASCII_NONZERO_DIGIT ~ (ASCII_DIGIT)* }

duration = { milliseconds | seconds | minutes | hours | days | weeks | years }
milliseconds = { "ms" }
seconds = { "s" }
minutes = { "m" }
hours = { "h" }
days = { "d" }
weeks = { "w" }
years = { "y" }

operator = { matches | not_matches | not_equals | equals }
matches = { "=~" }
not_matches = { "!~" }
not_equals = { "!=" }
equals = { "=" }

string = _{ "\"" ~ value ~ "\"" }
value = @{ char* }
char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}